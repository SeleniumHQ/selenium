load("//dotnet:defs.bzl", "nunit_test")

exports_files(["appconfig.json"])

SUPPORTED_BROWSERS = [
    ("chrome", "Chrome"),
    ("chromedev", "ChromeDev"),
    ("firefox", "Firefox"),
    ("firefoxnightly", "FirefoxNightly"),
    ("ie", "IE"),
    ("edge", "Edge"),
    ("safari", "Safari"),
    ("safaritechpreview", "SafariTechPreview"),
]

config_setting(
    name = "netframework",
    values = {"define": "framework=netfx"},
)

config_setting(
    name = "netcore",
    values = {"define": "framework=netcore"},
)

[nunit_test(
    name = "{}".format(target_name),
    size = "enormous",
    srcs = glob([
        "*.cs",
        "CustomDriverConfigs/*.cs",
        "CustomTestAttributes/*.cs",
        "DevTools/*.cs",
        "Environment/*.cs",
        "Interactions/*.cs",
    ]),
    out = "WebDriver.Common.Tests.dll",
    args = [
        "--workers=1",
        "--params=ConfigFile=$(location appconfig.json)",
        "--params=ActiveDriverConfig={}".format(config_setting),
    ],
    data = [
        "appconfig.json",
        "//common/src/web",
        "//java/test/org/openqa/selenium/environment:appserver_deploy.jar",
        "//java/test/org/openqa/selenium/environment:keystore",
        "//javascript/atoms",
        "//third_party/closure/goog",
        "//third_party/js/selenium:webdriver_json",
    ],
    is_windows = select({
        "@bazel_tools//src/conditions:host_windows": True,
        "//conditions:default": False,
    }),
    tags = [
        "no-sandbox",
        "requires-network",
    ],
    target_frameworks = select({
        ":netframework": ["net471"],
        ":netcore": ["netcoreapp3.1"],
        "//conditions:default": ["netcoreapp3.1"],
    }),
    visibility = ["//visibility:public"],
    deps = select({
        ":netframework": [
            "//dotnet/src/webdriver:net47",
            "@json.net//:Newtonsoft.Json",
            "@benderproxy//:BenderProxy",
            "@moq//:Moq",
        ],
        ":netcore": [
            "//dotnet/src/webdriver:netstandard2.1",
            "@json.net//:Newtonsoft.Json",
            "@benderproxy//:BenderProxy",
            "@moq//:Moq",
        ],
        "//conditions:default": [
            "//dotnet/src/webdriver:netstandard2.1",
            "@json.net//:Newtonsoft.Json",
            "@benderproxy//:BenderProxy",
            "@moq//:Moq",
        ],
    }),
) for (target_name, config_setting) in SUPPORTED_BROWSERS]
