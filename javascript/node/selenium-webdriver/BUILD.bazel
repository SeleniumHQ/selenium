load("@build_bazel_rules_nodejs//:index.bzl", "nodejs_binary", "pkg_npm")
load("@npm//@bazel/jasmine:index.bzl", "jasmine_node_test")
load("//common:defs.bzl", "copy_file")

SRC_FILES = [
    "CHANGES.md",
    "README.md",
    "package.json",
] + glob([
    "*.js",
    "example/*.js",
    "http/*.js",
    "io/*.js",
    "lib/*.js",
    "net/*.js",
    "remote/*.js",
    "testing/*.js",
    "devtools/*.js",
])

pkg_npm(
    name = "selenium-webdriver",
    srcs = SRC_FILES,
    deps = [
        ":license",
        "//javascript/node/selenium-webdriver/lib/atoms:find-elements",
        "//javascript/node/selenium-webdriver/lib/atoms:get_attribute",
        "//javascript/node/selenium-webdriver/lib/atoms:is_displayed",
    ],
)

TEST_FILES = glob(["test/**/*_test.js"])

TEST_DATA = SRC_FILES + glob(
    [
        "lib/test/**",
        "test/**",
    ],
    exclude = TEST_FILES,
)

nodejs_binary(
    name = "fileserver",
    data = TEST_DATA + [
        "//common/src/web",
        "@npm//express",
        "@npm//multer",
        "@npm//serve-index",
        "@npm//tmp",
        "@npm//jszip",
        "@npm//rimraf",
        "//javascript/node/selenium-webdriver/lib/atoms:get_attribute",
        "//javascript/node/selenium-webdriver/lib/atoms:is_displayed",
        "//javascript/node/selenium-webdriver/lib/atoms:find-elements",
    ],
    entry_point = "lib/test/fileserver.ts",
)

jasmine_node_test(
    name = "tests",
    srcs = TEST_FILES,
    data = TEST_DATA + [
        "tools/init_jasmine.js",
        "//common/src/web",
        "@npm//express",
        "@npm//multer",
        "@npm//serve-index",
        "@npm//jszip",
        "@npm//rimraf",
        "@npm//tmp",
        "@npm//sinon",
        "@npm//ws",
    ],
    local = True,
    templated_args = ["--node_options=--require=$$(rlocation $(rootpath :tools/init_jasmine.js))"],
    deps = [
        "//javascript/node/selenium-webdriver/lib/atoms:find-elements",
        "//javascript/node/selenium-webdriver/lib/atoms:get_attribute",
        "//javascript/node/selenium-webdriver/lib/atoms:is_displayed",
        "@npm//jasmine",
    ],
)

# npm_package does not pick up filegroup sources.
genrule(
    name = "license",
    srcs = ["//:license"],
    outs = [
        "LICENSE",
        "NOTICE",
    ],
    cmd = "cp $(locations //:license) $(@D)",
)

nodejs_binary(
    name = "cdp-srcs-generator",
    data = [
        ":browser_protocol",
        ":js_protocol",
    ],
    entry_point = "devtools/generator/protocol-dts-generator.js",
)

copy_file(
    name = "browser_protocol",
    src = "//common/devtools:browser_protocol.json",
    out = "devtools/generator/browser_protocol.json",
)

copy_file(
    name = "js_protocol",
    src = "//common/devtools:js_protocol.json",
    out = "devtools/generator/js_protocol.json",
)

genrule(
    name = "create-cdp-srcs",
    srcs = [
        ":browser_protocol",
        ":js_protocol",
    ],
    outs = [
        "devtools/generator/protocol.d.js",
        "devtools/generator/protocol-mapping.d.js",
        "devtools/generator/protocol-proxy-api.d.js",
    ],
    cmd = "$(location :cdp-srcs-generator) $(location :browser_protocol) $(location :js_protocol) $(OUTS)",
    tools = [
        ":cdp-srcs-generator",
    ],
)
