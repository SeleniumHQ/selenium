load("@crates//:defs.bzl", "all_crate_deps")
load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_library", "rust_test")

rust_binary(
    # Yes, this name is very similar to the library. Note the dash
    # instead of an underscore
    name = "selenium-manager",
    srcs = ["src/main.rs"],
    edition = "2021",
    visibility = ["//visibility:public"],
    deps = [
        ":selenium_manager",
    ] + all_crate_deps(normal = True),
)

rust_library(
    # The name here is used as the crate name
    name = "selenium_manager",
    srcs = glob(
        ["src/**/*.rs"],
        exclude = ["main.rs"],
    ),
    edition = "2021",
    deps = all_crate_deps(normal = True),
)

rust_test(
    name = "unit",
    size = "small",
    srcs = glob(["src/**/*.rs"]),
    crate = ":selenium_manager",
    tags = ["no-sandbox"],
)

[
    rust_test(
        name = file[:-3],
        size = "small",
        srcs = [file],
        data = [":selenium-manager"],
        edition = "2021",
        tags = [
            "exclusive",
            "no-sandbox",
            "requires-network",
        ],
        deps = [":selenium_manager"] + all_crate_deps(
            normal = True,
            normal_dev = True,
        ),
    )
    for file in glob(["tests/**/*.rs"])
]
