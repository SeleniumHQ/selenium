load("@rules_jvm_external//:defs.bzl", "artifact")
load("//:copy_file.bzl", "copy_file")
load("//java:defs.bzl", "java_export")
load("//java:version.bzl", "SE_VERSION")

GENERATOR_SOURCES = [
    "CdpClientGenerator.java",
]

PROTOTYPE_SOURCES = [
    "Command.java",
    "ConverterFunctions.java",
    "DevToolsException.java",
    "Event.java",
]

java_library(
    name = "devtools-prototypes",
    srcs = PROTOTYPE_SOURCES,
    deps = [
        "//java/client/src/org/openqa/selenium:core",
        "//java/client/src/org/openqa/selenium/json",
        artifact("com.google.guava:guava"),
    ],
)

java_export(
    name = "devtools",
    srcs = glob(
        ["*.java"],
        exclude = PROTOTYPE_SOURCES + GENERATOR_SOURCES,
    ),
    maven_coordinates = "org.seleniumhq.selenium:selenium-devtools:%s" % SE_VERSION,
    pom_template = "//java/client/src/org/openqa/selenium:template-pom",
    visibility = [
        "//visibility:public",
    ],
    exports = [
        ":cdp",
        ":devtools-prototypes",
    ],
    deps = [
        ":cdp",
        ":devtools-prototypes",
        "//java:auto-service",
        "//java/client/src/org/openqa/selenium:core",
        "//java/client/src/org/openqa/selenium/json",
        "//java/client/src/org/openqa/selenium/remote",
        artifact("com.google.guava:guava"),
    ],
)

java_binary(
    name = "cdp-client-generator",
    srcs = GENERATOR_SOURCES,
    main_class = "org.openqa.selenium.devtools.CdpClientGenerator",
    deps = [
        "//java/client/src/org/openqa/selenium:core",
        "//java/client/src/org/openqa/selenium/devtools:devtools-prototypes",
        "//java/client/src/org/openqa/selenium/json",
        artifact("com.github.javaparser:javaparser-core"),
        artifact("com.google.guava:guava"),
    ],
)

genrule(
    name = "create-cdp-srcs",
    srcs = [
        "//java/client/src/org/openqa/selenium/devtools:browser_protocol",
        "//java/client/src/org/openqa/selenium/devtools:js_protocol",
    ],
    outs = ["cdp.srcjar"],
    cmd = "$(location :cdp-client-generator) $(location //java/client/src/org/openqa/selenium/devtools:js_protocol) $(location //java/client/src/org/openqa/selenium/devtools:browser_protocol) $@",
    tools = [
        ":cdp-client-generator",
    ],
)

java_library(
    name = "cdp",
    srcs = [
        ":create-cdp-srcs",
    ],
    deps = [
        "//java/client/src/org/openqa/selenium:core",
        "//java/client/src/org/openqa/selenium/devtools:devtools-prototypes",
        "//java/client/src/org/openqa/selenium/json",
        artifact("com.google.guava:guava"),
    ],
)

copy_file(
    name = "browser_protocol",
    src = "//common/devtools:browser_protocol.json",
    out = "browser_protocol.json",
)

copy_file(
    name = "js_protocol",
    src = "//common/devtools:js_protocol.json",
    out = "js_protocol.json",
)
